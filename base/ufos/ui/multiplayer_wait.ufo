// ===========================================
// MULTIPLAYER WAIT DIALOG
// ===========================================

component cmpWaitPanel	extends	panel
{
	{
		size		"400 250"
		bgcolor		"0 0 0 0.9"
		border		2
		bordercolor	"0.56 0.81 0.76 1"
	}

	string title
	{
		pos		"0 0"
		size	"400 30"
		contentalign	ALIGN_CL
		font		"f_small_bold"
		bgcolor		"0.4 0.515 0.5 0.25"
		color		"0.56 0.81 0.76 1"
	}

	fuzzyScreen overlay { }
}

window multiplayer_wait
{
	{
		fill		true
		starlayout	true
	}

	timer event {
		timeout 	2000
		// update every 2 sec
		onEvent {
			cmd "mp_selectteam_init;"
		}
	}

	battlescape render {
		pos			"0 0"
		size		"1024 768"
		align		LAYOUTALIGN_FILL
	}

	WorldLevel level {
		{
			pos			"90 10"
		}
	}

	cmpWaitPanel players {
		{
			pos			"520 60"
			padding		5
		}

		string title
		{
			string		"_Players connected"
			width		"400"
			contentalign	ALIGN_CC
		}

		panel ready {
			{
				layout LAYOUT_TOP_DOWN_FLOW
				layoutMargin 2
				padding		0
				pos			"50 50"
				size		"16 200"
			}

			image p0 { size "16 16" }
			image p1 { size "16 16" }
			image p2 { size "16 16" }
			image p3 { size "16 16" }
			image p4 { size "16 16" }
			image p5 { size "16 16" }
			image p6 { size "16 16" }
			image p7 { size "16 16" }
			image p8 { size "16 16" }
			image p9 { size "16 16" }
		}

		text userlist
		{
			dataid		TEXT_MULTIPLAYER_USERLIST
			pos			"80 50"
			size		"150 200"
		}

		text teamlist
		{
			dataid		TEXT_MULTIPLAYER_USERTEAM
			pos			"240 50"
			size		"120 200"
		}
	}

	cmpWaitPanel teamselection {
		{
			pos			"520 320"
			size		"400 150"
			padding		10
		}

		string title
		{
			string		"_Team selection"
			contentalign	ALIGN_CC
		}

		text teamnum_status
		{
			dataid		TEXT_STANDARD
			pos			"10 40"
			size		"380 40"
			tabwidth	60
			contentalign	ALIGN_CC
		}

		string teamnum_str
		{
			string		"_Team preference:"
			pos			"25 85"
			size		"195 20"
		}

		string teamnum_value
		{
			string		*cvar:cl_teamnum
			pos			"248 85"
			size		"40 20"
			contentalign	ALIGN_UR
		}

		button teamnum_inc
		{
			visiblewhen	"*cvar:cl_ready != 1"
			tooltip		"_Next free team"
			icon	"icons/arrow_right"
			onMouseEnter { *node:this@icon = "icons/arrow_right_on" }
			onMouseLeave { *node:this@icon = "icons/arrow_right" }
			pos			"298 87"
			size		"16 16"
			padding		0
			onClick		{ cmd "teamnum_inc;" }
		}

		button teamnum_dec
		{
			visiblewhen	"*cvar:cl_ready != 1"
			tooltip		"_Previous free team"
			icon		"icons/arrow_left"
			onMouseEnter { *node:this@icon = "icons/arrow_left_on" }
			onMouseLeave { *node:this@icon = "icons/arrow_left" }
			pos			"222 87"
			size		"16 16"
			padding		0
			onClick		{ cmd "teamnum_dec;" }
		}

		CheckBox cb_ready
		{
			tooltip		"_Finish the teamselection and start the game"
			pos			"200 112"
			size		"20 20"
			current		*cvar:cl_ready
		}

		string cb_ready_label
		{
			string		"_Are you ready?"
			tooltip		"_Finish the teamselection and start the game"
			pos			"230 110"
			size		"165 20"
			onClick		{ call *node:parent.cb_ready@toggle }
		}
	}

	cmpWaitPanel admin
	{
		{
			pos		"50 60"
			size	"400 200"
			padding	10
		}

		string title
		{
			string		"_Admin control"
			contentalign	ALIGN_CC
		}

		string maxperplayer_str
		{
			string		"_Max. soldiers per team:"
			tooltip		"_Sets the max allowed soldiers per team"
			pos			"25 60"
			size		"250 20"
		}

		string maxperplayer_value
		{
			string		*cvar:sv_maxsoldiersperteam
			pos			"280 60"
			size		"40 20"
			contentalign	ALIGN_UC
		}

		Spinner maxperplayer_change
		{
			current		*cvar:sv_maxsoldiersperteam
			pos			"325 60"
			min			1
			max			12
			delta		1
		}

		string maxsoldierperplayer_str
		{
			string		"_Max. soldiers per player:"
			tooltip		"_Sets the max allowed soldiers per player when in teamplay"
			pos			"25 85"
			size		"250 20"
		}

		string maxsoldierperplayer_value
		{
			string		*cvar:sv_maxsoldiersperplayer
			pos			"280 85"
			size		"40 20"
			contentalign	ALIGN_UC
		}

		Spinner maxsoldierperplayer_change
		{
			current		*cvar:sv_maxsoldiersperplayer
			pos			"325 85"
			min			1
			max			12
			delta		1
		}

		string roundlimit_str
		{
			string		"_Round time limit:"
			tooltip		"_Sets the max allowed soldiers per team"
			pos			"25 110"
			size		"250 20"
		}

		string roundtimelimit_value
		{
			string		*cvar:sv_roundtimelimit
			pos			"280 110"
			size		"40 20"
			contentalign	ALIGN_UC
		}

		Spinner roundtimelimit_change
		{
			current		*cvar:sv_roundtimelimit
			pos			"325 110"
			min			30
			max			500
			delta		5
		}

		MainMenuBtn button_force
		{
			string		"_Force start"
			tooltip		"_Launch the game before all teams are ready"
			pos			"125 145"
			size		"150 30"
			color		"0.8 0.8 0.8 1"
			selectcolor "1 1 1 1"
			onClick		{ cmd "cl_startgame;" }
		}
	}

	cmpWaitPanel rcon
	{
		{
			pos		"50 280"
			size	"400 110"
			padding	10
		}

		string title
		{
			string		"_Remote Admin Control"
			contentalign	ALIGN_CC
		}

		string current_rcon_pwd
		{
			string		"_RCON-Password:"
			pos			"25 30"
			size		"256 30"
		}

		TextEntry slot_rcon_pwd
		{
			string		*cvar:rcon_password
			font		"f_verysmall"
			pos			"25 65"
			size		"250 30"
			ispassword	true
		}
	}

	fuzzyScreen overlay { }

	confunc multiplayer_admin_panel {
		if (<1> == 0) {
			*node:root.admin@invis = true
		} else {
			*node:root.admin@invis = false
		}
	}

	/**
	 * @param <1> player ID/line
	 * @param <2> 0: not ready, 1: ready
	 * @todo use an image instead of a color
	 */
	confunc multiplayer_playerIsReady {
		if ( ( <1> < 10 ) and ( <1> >= 0 ) ) {
			if ( <2> == 0 ) {
				*node:root.players.ready.p<1>@src = "icons/off"
				*node:root.players.ready.p<1>@invis = false
			} else {
				*node:root.players.ready.p<1>@src = "icons/on"
				*node:root.players.ready.p<1>@invis = false
			}
		}
	}

	/**
	 * @param <1> number of players
	 */
	confunc multiplayer_playerNumber {
		*cvar:tmp = "<1>"
		while ( *cvar:tmp < 10 ) {
			*node:root.players.ready.p<1>@invis = true
			*cvar:tmp = ( *cvar:tmp + 1 )
		}
		delete *cvar:tmp
	}

	/**
	 * @param <1> boolean whether this is a teamplay match
	 */
	confunc multiplayer_setTeamplay {
		// @todo - deactivate team change if the given boolean is false	(0)
	}

	// ===================
	// EVENTS
	// ===================

	func onWindowOpened
	{
		cmd "mp_selectteam_init;ui_push hud_chat;"
		*cvar:mn_displaypartyinfo = 1
		*cvar:mn_entry = ""
		*cvar:mn_chatselect = "chat"
		*cvar:cl_ready = 0
	}

	func onWindowClosed
	{
		delete *cvar:mn_entry
		delete *cvar:mn_chatselect
		delete *cvar:mn_displaypartyinfo
	}
}
